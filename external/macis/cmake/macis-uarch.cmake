if(DEFINED MACIS_UARCH)
  message(STATUS "Using user-defined uarch: ${MACIS_UARCH}")
  # If compiler ID is not GNU or Clang, we cannot use -march flag, so we will not set MACIS_UARCH_FLAGS
  if(NOT (CMAKE_CXX_COMPILER_ID MATCHES "GNU|Clang|AppleClang"))
    message(WARNING "Compiler ${CMAKE_CXX_COMPILER_ID} does not support -march flag. MACIS_UARCH_FLAGS will not be set.")
    set(MACIS_UARCH_USED "NONE" CACHE STRING "User-defined microarchitecture for optimization")
  else()
    set(MACIS_UARCH_USED ${MACIS_UARCH} CACHE STRING "User-defined microarchitecture for optimization")
    set(MACIS_UARCH_FLAGS "-march=${MACIS_UARCH}" CACHE STRING "Compiler flags for user-defined microarchitecture")
  endif()
else()
  message(WARNING "MACIS_UARCH not defined. This may degrade performance")
  set(MACIS_UARCH_USED "NONE" CACHE STRING "User-defined microarchitecture for optimization")
endif()

# Test that the produced flags are sane
if(MACIS_UARCH_FLAGS)
  message(STATUS "Testing MACIS_UARCH_FLAGS: ${MACIS_UARCH_FLAGS}")
  include(CheckCXXCompilerFlag)
  check_cxx_compiler_flag("${MACIS_UARCH_FLAGS}" COMPILER_SUPPORTS_MACIS_UARCH_FLAGS)
  if(NOT COMPILER_SUPPORTS_MACIS_UARCH_FLAGS)
    message(WARNING "The compiler does not support the specified MACIS_UARCH_FLAGS: ${MACIS_UARCH_FLAGS}. Unsetting these flags.")
    unset(MACIS_UARCH_FLAGS CACHE)
    set(MACIS_UARCH_USED "NONE" CACHE STRING "User-defined microarchitecture for optimization")
  endif()
endif()
